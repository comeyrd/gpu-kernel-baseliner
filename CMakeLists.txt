cmake_minimum_required(VERSION 3.15)
project(baseliner VERSION 0.8.0 LANGUAGES CXX)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
option(BASELINER_BUILD_EXAMPLES "Build baseliner examples" OFF)
set(BASELINER_FOUND_DEVICE_COMPILER OFF)


include(CheckLanguage)
check_language(CUDA)
if(CMAKE_CUDA_COMPILER)
    set(CMAKE_CUDA_RUNTIME_LIBRARY Shared)
    enable_language(CUDA)
    if(CMAKE_CUDA_COMPILER_VERSION VERSION_LESS 11.0)
        message(FATAL_ERROR "Baseliner requires CUDA 11.0 or greater (found ${CMAKE_CUDA_COMPILER_VERSION})")
    endif()
    set(BASELINER_HAS_CUDA ON)
    set(BASELINER_FOUND_DEVICE_COMPILER ON)
    find_package(CUDAToolkit REQUIRED)
    if(CUDAToolkit_FOUND)
        message(STATUS "Found CUDA Toolkit Version: ${CUDAToolkit_VERSION}")
        if(TARGET CUDA::nvml)
            set(BASELINER_HAS_NVML ON)
            add_compile_definitions(BASELINER_HAS_NVML)
        endif()
endif()
endif()

check_language(HIP)
if(CMAKE_HIP_COMPILER)
    enable_language(HIP)
    if(CMAKE_HIP_COMPILER_VERSION VERSION_LESS 5.2)
        message(FATAL_ERROR "Baseliner requires ROCm (HIP) 5.2 or greater (found ${CMAKE_HIP_COMPILER_VERSION})")
    endif()
    set(BASELINER_HAS_HIP ON)
    set(BASELINER_FOUND_DEVICE_COMPILER ON)
    set(ROCM_PATH "/opt/rocm" CACHE PATH "Path to ROCm installation")
        find_path(AMDSMI_INCLUDE_DIR NAMES amd_smi/amdsmi.h PATHS ${ROCM_PATH}/include)
    find_library(AMDSMI_LIBRARY NAMES amd_smi PATHS ${ROCM_PATH}/lib)

    if(AMDSMI_INCLUDE_DIR AND AMDSMI_LIBRARY)
        message(STATUS "Found AMD SMI: ${AMDSMI_LIBRARY}")  
        set(BASELINER_HAS_AMDSMI ON)
        add_compile_definitions(BASELINER_HAS_AMDSMI) 
        add_library(AMD::amdsmi UNKNOWN IMPORTED)
        set_target_properties(AMD::amdsmi PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${AMDSMI_INCLUDE_DIR}"
            IMPORTED_LOCATION "${AMDSMI_LIBRARY}"
        ) 
    endif()
endif()

if(NOT BASELINER_FOUND_DEVICE_COMPILER)
    message(FATAL_ERROR "Baseliner requires at least one device device compiler")
endif()
#TEMPORARY FIX AS THROUGH PRESETS IT DOES NOT WORK
set(CMAKE_CUDA_USE_RESPONSE_FILE_FOR_INCLUDES 0)
set(CMAKE_CUDA_USE_RESPONSE_FILE_FOR_LIBRARIES 0)
set(CMAKE_CUDA_USE_RESPONSE_FILE_FOR_OBJECTS 0)

add_subdirectory(baseliner)
if(BASELINER_BUILD_EXAMPLES)
    add_subdirectory(examples)
endif()